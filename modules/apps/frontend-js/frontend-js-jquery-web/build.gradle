import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildBootstrapJS(type: Copy)
task buildJQuery(type: Copy)
task buildJQueryForm(type: Copy)
task buildPopperJS(type: Copy)

File jsDestinationDir = file("tmp/META-INF/resources")
File jqueryDestinationDir = new File(jsDestinationDir, "jquery")

buildBootstrapJS {
	dependsOn buildJQuery

	eachFile new StripPathSegmentsAction(3)

	from "node_modules"

	include "bootstrap/dist/js/bootstrap.bundle.min.js"
	include "bootstrap/dist/js/bootstrap.bundle.min.js.map"

	includeEmptyDirs = false
	into jqueryDestinationDir
}

buildJQuery {
	dependsOn npmInstall

	doFirst {
		delete jqueryDestinationDir
	}

	eachFile new StripPathSegmentsAction(2)

	from npmInstall.nodeModulesDir

	include "jquery/dist/jquery.min.js"
	include "jquery/dist/jquery.min.js.map"

	includeEmptyDirs = false
	into jqueryDestinationDir
}

buildJQueryForm {
	dependsOn buildJQuery

	eachFile new StripPathSegmentsAction(2)

	from npmInstall.nodeModulesDir

	include "@liferay/jquery-form/jquery.form.js"

	includeEmptyDirs = false
	into jqueryDestinationDir

	rename "jquery.form.js", "form.js"
}

buildPopperJS {
	dependsOn buildJQuery

	eachFile new StripPathSegmentsAction(3)

	from npmInstall.nodeModulesDir

	include "popper.js/dist/umd/popper.min.js"
	include "popper.js/dist/umd/popper.min.js.map"

	includeEmptyDirs = false
	into jqueryDestinationDir
}

classes {
	dependsOn buildBootstrapJS
	dependsOn buildJQuery
	dependsOn buildJQueryForm
	dependsOn buildPopperJS
}

clean {
	delete "tmp"
}

dependencies {
	compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bnd.annotation", version: "4.2.0"
	compileOnly group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "4.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.osgi.util", version: "5.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "3.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.configuration.metatype.api", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portal.url.builder.api", version: "2.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "4.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "4.34.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "4.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.1"
	compileOnly group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component", version: "1.3.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
}